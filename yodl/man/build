#!/usr/bin/icmake -qt/tmp/yodl

string bin;
string inc;
string scripts;
string root;
string cwd;
string yodl;
string yodlpost;
string config;
string yodlCmd;

#include "../src/config.h"

void preset(string toYodlRoot)
{
    cwd     = chdir(".");
    root    =  chdir(toYodlRoot);

    bin =       root + "src/bin/";
    scripts =   root + "scripts/";

    config      = root  + "src/config.h";
    yodl        = bin   + "yodl";
    yodlpost    = bin   + "yodlpost";
    inc         = ".:" + root + "macros/yodl";

    if (!exists("release.yo"))
        fprintf("release.yo", "SUBST(_CurVers_)(", TOPLEVEL_VERSION, ")\n",
                              "SUBST(_CurYrs_)(", YODL_YEARS, ")\n");
    chdir(cwd);
}

void run(string source, string nr, int usePath)
{
    exec(scripts + "configreplacements", config, 
                            source + ".in", 
                            source + ".yo");

    
    exec("mkdir", "-p", nr);

    if (usePath)
        exec("yodl2man", "-o", nr + "/" + source + "." + nr, source);
    else
    {
        exec(yodl, "-D", "XXMACROPATH=.", "-I", inc, "-o", "out", "man", 
                                                                    source);
        exec(yodlpost, "out.idx", "out", nr + "/" + source + "." + nr);
    }

    exec("rm", "-f", "out", "out.idx", source + ".yo");
}


void manpages(string path)
{
    int usePath;

    usePath = path == "path";

    run("yodl",             "1", usePath);
    run("yodlpost",         "1", usePath);
    run("yodlconverters",   "1", usePath);
    run("yodlverbinsert",   "1", usePath);

    run("yodlmanpage",  "7", usePath);
    run("yodlletter",   "7", usePath);
    run("yodlmacros",   "7", usePath);
    run("yodlbuiltins", "7", usePath);
}

void install(string where)
{
    list l;
    int idx;

    exec("mkdir", "-p", where + "/man1", where + "/man7");
    exec("cp", "1/*", where + "/man1");
    exec("cp", "7/*", where + "/man7");

    chdir(where + "/man1");

    l = strtok(STD_CONVERSIONS, " ");

    for (idx = 0; idx < sizeof(l); idx++)
        exec("ln", "-sf", "yodlconverters.1.gz", 
                                        "yodl2" + element(idx, l) + ".1.gz");
    exec("ln", "-sf", "yodlconverters.1.gz", 
                                    "yodl2whatever.1.gz");
}
    
void cleanup()
{
    system("rm -rf 1 7 *.yo");
}

void main(int argc, list argv)
{
    string arg1;

    if (argc == 1)
    {
        printf
        (
            "\n"
            "Build what? Options are:\n"
            "   clean: clean up\n"
            "   install where: install in the directory `where'\n"
            "   man [path]: build the manual pages (after `clean')\n"
            "               (with `path': use yodl found in $PATH\n" 
            "\n"
        );
        exit(1);
    }

    preset("..");

    arg1 = argv[1];

    if (arg1 == "man")
        manpages(argv[2]);
    else if (arg1 == "clean")
        cleanup();
    else if (arg1 == "install")
        install(argv[2]);
    else
    {
        printf("request `", arg1, "' not yet available\n");
        exit(1);
    }
    exit(0);
}
